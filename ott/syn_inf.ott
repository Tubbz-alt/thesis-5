%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Inference  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

metavar au {{ tex \alpha }}, bu {{ tex \beta }} ::=
metavar cu {{ tex \iota }} ::=

grammar

aus {{ tex \overline{\alpha} }}, bus {{ tex \overline{\beta} }}
  :: 'UnifVars_' ::=
| au                        ::   :: One
| aus1 , .... , ausi        ::   :: Many
| nil                       :: M :: Nil

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Contexts  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

% A context containing only unification variables
unif_bnd :: 'IUnifBnd_' ::=
| au : k             ::   :: Ty
  {{ tex [[au]] {:} [[k]] }}
| cu : phi           ::   :: Co
  {{ tex [[cu]] {:} [[phi]] }}
| a :g k             ::   :: GivenTy
  {{ tex [[a]] {:}_{\mathsf{g} } [[k]] }}
| c :g phi           ::   :: GivenCo
  {{ tex [[c]] {:}_{\mathsf{g} } [[phi]] }}
| a :c k             ::   :: GivenClass
  {{ tex [[a]] {:}_{\mathsf{c} } [[k]] }}

O {{ tex \Omega }}, Os {{ tex \overline{\Omega} }} :: 'IUnifCtx_' ::=
| unif_bnd              ::   :: One
| O1 , .... , Oi        ::   :: Many
| nil                   :: M :: Nil
| aus : D               :: M :: TelBind
  {{ tex [[aus]]{:}[[D]] }}
| aus : ts             :: M :: KsBind
  {{ tex [[aus]]{:}[[ts]] }}
| g( D )               :: M :: GivenTel
  {{ tex \mathsf{g}([[D]]) }}
| ( O )                :: M :: Parens

P {{ tex \Psi }} :: 'ICtx_' ::=
| d                  ::   :: Binder
| O                  ::   :: UnifBinders
| P1 , .... , Pi     ::   :: Many
| nil                :: M :: Nil
| G                  :: M :: PlainCtx
| Rel ( P )          :: M :: Reset
  {{ tex [[Rel]]([[P]]) }}

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%  Judgments  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

ijudg :: 'IJudg_' ::=
| S ; P |=ty t : k                    ::   :: Ty
| S ; P |=co g : phi                  ::   :: Co
| S ; P |=vec ps : P2                 ::   :: Vec
| S ; P ; s ; t |=alt alt : k         ::   :: Alt
  {{ tex [[S]];[[P]];[[s]] [[|=alt]]^{\!\!\!\raisebox{.1ex}{$\scriptstyle [[t]]$} } [[alt]] : [[k]] }}
| S |=ctx P                           ::   :: Ctx
  {{ tex [[S]] [[|=ctx]] [[P]] \ok }}
